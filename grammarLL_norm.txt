NT_PROGRAM -> NT_STMTLIST

NT_STMTLIST -> NT_STMT NT_STMTLIST
NT_STMTLIST -> EPSILON

NT_STMT -> T_IMPORT T_ID T_ENDL
NT_STMT -> T_ENDL
NT_STMT -> NT_ASSIGNMENT T_ENDL
NT_STMT -> NT_T_FUNCTIONDEF T_ENDL
NT_STMT -> NT_IF T_ENDL
NT_STMT -> NT_FORLOOP T_ENDL
NT_STMT -> NT_WHILELOOP T_ENDL
NT_STMT -> NT_INDEFINITELOOP T_ENDL
NT_STMT -> T_RETURN NT_EXPR T_ENDL
NT_STMT -> T_BREAK T_ENDL
NT_STMT -> T_CONTINUE T_ENDL

NT_T_FUNCTIONDEF -> T_FUNCTION T_ID NT_PARAMETERS T_ENDL NT_STMTLIST T_END

NT_PARAMETERS -> T_LPARENS NT_PARAMETERS1
NT_PARAMETERS1 -> T_RPARENS
NT_PARAMETERS1 -> NT_PARAMDECL T_RPARENS

NT_PARAMDECL -> T_ID NT_PARAMDECL1
NT_PARAMDECL1 -> T_COMMA T_ID NT_PARAMDECL1
NT_PARAMDECL1 -> EPSILON 

NT_IF -> T_IF NT_CONDITION T_THEN T_ENDL NT_STMTLIST NT_ELSEIF1
NT_ELSEIF1 -> T_ELSE NT_ELSEIF2
NT_ELSEIF1 -> T_END
NT_ELSEIF2 -> T_ENDL NT_STMTLIST T_END
NT_ELSEIF2 -> NT_IF

NT_FORLOOP -> T_FOR T_ID T_ASSIGN NT_ARITHMETIC T_TO NT_ARITHMETIC T_DO T_ENDL NT_STMTLIST T_LOOP

NT_WHILELOOP -> T_WHILE NT_CONDITION T_DO T_ENDL NT_STMTLIST T_LOOP

NT_INDEFINITELOOP -> T_ITERATE T_ENDL NT_STMTLIST T_LOOP

NT_ASSIGNMENT -> NT_LVALUE T_ASSIGN NT_CONDITION

NT_EXPR -> NT_CONDITION

NT_CONDITION -> NT_LOGIC NT_CONDITION1
NT_CONDITION1 -> T_AND NT_LOGIC NT_CONDITION1
NT_CONDITION1 -> T_OR NT_LOGIC NT_CONDITION1
NT_CONDITION1 -> EPSILON

NT_LOGIC -> T_NOT NT_COMPARISON
NT_LOGIC -> NT_COMPARISON

NT_COMPARISON -> NT_ARITHMETIC NT_COMPARISON1
NT_COMPARISON1 -> T_EQ NT_ARITHMETIC NT_COMPARISON1
NT_COMPARISON1 -> T_NEQ NT_ARITHMETIC NT_COMPARISON1
NT_COMPARISON1 -> T_LT NT_ARITHMETIC NT_COMPARISON1
NT_COMPARISON1 -> T_GT NT_ARITHMETIC NT_COMPARISON1
NT_COMPARISON1 -> T_LE NT_ARITHMETIC NT_COMPARISON1
NT_COMPARISON1 -> T_GE NT_ARITHMETIC NT_COMPARISON1
NT_COMPARISON1 -> EPSILON

NT_ARITHMETIC -> NT_TERM NT_ARITHMETIC1
NT_ARITHMETIC1 -> T_PLUS NT_TERM NT_ARITHMETIC1
NT_ARITHMETIC1 -> T_MINUS NT_TERM NT_ARITHMETIC1
NT_ARITHMETIC1 -> EPSILON

NT_TERM -> NT_FACTOR NT_TERM1
NT_TERM1 -> T_TIMES NT_FACTOR NT_TERM1
NT_TERM1 -> T_DIV NT_FACTOR NT_TERM1
NT_TERM1 -> EPSILON

NT_FACTOR -> T_MINUS NT_FACTOR
NT_FACTOR -> T_NEG NT_FACTOR
NT_FACTOR -> NT_FINAL

NT_FINAL -> T_LPARENS NT_EXPR T_RPARENS
NT_FINAL -> NT_BOOLEAN
NT_FINAL -> T_INT
NT_FINAL -> T_FLOAT
NT_FINAL -> T_STRING
NT_FINAL -> NT_OBJECT
NT_FINAL -> NT_REFERENCE
NT_FINAL -> T_NILL

NT_LVALUE -> T_ID NT_LVALUE1
NT_LVALUE1 -> EPSILON
NT_LVALUE1 -> T_DOT T_ID NT_LVALUE1
NT_LVALUE1 -> T_LBRACKET NT_EXPR T_RBRACKET NT_LVALUE1

NT_REFERENCE -> NT_LVALUE NT_REFERENCE1
NT_REFERENCE1 -> EPSILON
NT_REFERENCE1 -> T_LPARENS NT_REFERENCE2
NT_REFERENCE2 -> T_RPARENS NT_REFERENCE1
NT_REFERENCE2 -> NT_ARGUMENTS T_RPARENS NT_REFERENCE1

NT_ARGUMENTS -> NT_EXPR NT_ARGUMENTS1
NT_ARGUMENTS1 -> EPSILON
NT_ARGUMENTS1 -> T_COMMA NT_EXPR NT_ARGUMENTS1

NT_OBJECT -> T_LBRACKET NT_OBJECT1
NT_OBJECT -> T_LBRACES NT_DICTIONARYINIT T_RBRACES
NT_OBJECT1 -> T_RBRACKET
NT_OBJECT1 -> NT_ARRAYINIT T_RBRACKET

NT_ARRAYINIT -> NT_EXPR NT_ARRAYINIT1
NT_ARRAYINIT1 -> EPSILON
NT_ARRAYINIT1 -> T_COMMA NT_EXPR NT_ARRAYINIT1

NT_DICTIONARYINIT -> T_ID T_COLON NT_EXPR NT_DICTIONARYINIT1
NT_DICTIONARYINIT1 -> EPSILON
NT_DICTIONARYINIT1 -> T_COMMA T_ID T_COLON NT_EXPR NT_DICTIONARYINIT1

NT_BOOLEAN -> T_TRUE
NT_BOOLEAN -> T_FALSE

